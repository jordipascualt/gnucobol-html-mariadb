      *> ***************************************************************
      *> Author:    Jordi Pascual
      *> About:     GNUCobol + HTML + MariaDB (Mysql).
      *> Date:      dec-2024
      *> License:   Public Domain 
      *> Usage:     Users Maintemance (CRUD).
      *> https://github.com/jordipascualt/gnucobol-html-mariadb
      *> ***************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. COBNEWUSR.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       DATA DIVISION.
       FILE SECTION.
       WORKING-STORAGE SECTION.
       EXEC SQL 
         BEGIN DECLARE SECTION 
       END-EXEC
       01  HOSTVARS.
           05 BUFFER               PIC X(1024).
           05 XSQL1                PIC X.
           05 XSQL16               PIC X(16).
           05 XSQL32               PIC X(32).
           05 XNUM12               PIC 9(12).
           05 HCAMPO               PIC X(32).
           05 PWDFLD               PIC X(32).
           05 USER                 PIC X(32).
           05 PAGEH                PIC X(65365).
           05 SESIX                PIC X(32).
           05 TABLA                PIC X(18).

       01  USERS.
           05 COMMENT              PIC X(128).
           05 AGREE                PIC X.
           05 AGE                  PIC X(16).
           05 TIPO                 PIC X(16).
           05 TIMEHMS              PIC X(8).
           05 DATEDMY              PIC X(10).
           05 PWD                  PIC X(32).
           05 RPWD                 PIC X(32).
           05 USERNAME             PIC X(32).
           05 EMAIL                PIC X(32).
           05 IDENT                PIC X(11).
           05 NAME                 PIC X(64).
       01  SYSFLD.
           05 IDFLD                PIC 9(11).
           05 TNXFLD               PIC 9(11).
           05 MAPFLD               PIC X(16).
           05 FLD                  PIC X(32).
           05 LENFLD               PIC 9(11).
           05 COMFLD               PIC X(128).
           05 VALFLD               PIC X(128).
       01  ROWLOCK.
           05 IDRWL                PIC 9(11).
           05 TABRWL               PIC X(32).
           05 RWRWL                PIC 9(11).
           05 USRRWL               PIC X(32).
           05 SESRWL               PIC X(32).
           05 STARWL               PIC X(18).
       EXEC SQL
          END DECLARE SECTION 
       END-EXEC.
       01  NEWCODI                 PIC 9(11).
       01  WEB-STATUS              PIC 99.
       01  RESULTADO               PIC X(256).
       01  OPERACION               PIC X(50).
       01  ALERTA                  PIC X(256).
       01  newline                 PIC X value x'0a'.
       01  PAGEA                   PIC X(63535).
       01  INDIFIN                 PIC 9 VALUE 0.
       01  IX                      PIC 9999 VALUE 0.
       01  IT                      PIC 99 VALUE 0.
       01  DATETIME                PIC X(21).
       01  XSTAMP                  PIC X(19).
       01  SES                     PIC X(32).
       01  LASTTNX                 PIC 9(11).
       01  BUTTONCK                PIC X(128).
       01  PAGES                   PIC X(65365).

       01  SYSCOMM2. 
           05 FILLER               PIC X(17).
           05 IDET2                PIC X(11).
           05 FILLER               PIC X(17).

       01  SYSCOMMALL              EXTERNAL.
           05 SYSCOMM.
             07 ACCTN              PIC X.
             07 PAGEX              PIC X(16).
             07 IDET               PIC X(11).
             07 PAGEACTIVE         PIC X.
             07 PROGID             PIC X(16).
             07 USERN              PIC X(32).
             07 SESSION            PIC X(32).
           05 LIMITE               PIC 9(10).
           05 SALTAR               PIC 9(10).
           05 BUSCAR               PIC X(32).
           05 PAGETOSEND           PIC X(63535).

     
       PROCEDURE DIVISION.
       COBNEWUSR.
      *> Always send out the Content-type before any other IO
       display
           "Content-type: text/html"
           newline
       end-display

         MOVE SPACES TO SYSCOMM.
         MOVE "sysusers" TO TABLA.
         ACCEPT SYSCOMM FROM COMMAND-LINE.
      *   DISPLAY SYSCOMM.
         INSPECT SYSCOMM REPLACING ALL "_" BY " ".
         IF ACCTN < '*' 
            MOVE 'C' TO ACCTN
            MOVE 'USERS' TO PAGEX
            MOVE 0 TO IDET
            MOVE 'N' TO PAGEACTIVE
            MOVE 'COBNEWUSR.EXE' TO PROGID.
         STRING 'DRIVER={MariaDB};'
                'SERVER=localhost;'
                'PORT=3306;'
                'DATABASE=cobolhtml;'
                'USER=*****;'
                'PASSWORD=*****;'
         INTO BUFFER.
         EXEC SQL 
           CONNECT TO :BUFFER 
         END-EXEC
         PERFORM SQLERRORES.
         MOVE SPACES TO PAGETOSEND.
         MOVE SPACES TO ALERTA, STARWL, BUFFER.
         PERFORM CHECKROWLOCK.
           IF STARWL > '*'
              STRING '* THE INFORMATION REQUESTED IS LOCKED'
                     ' BY USER: '
                     FUNCTION TRIM(USRRWL), '\n SESSION: ', SESRWL 
                     '\n ON: ' STARWL
                     '\n * PLEASE AKS THE USER TO UNLOCK'
                      ' OR TRY AGAIN LATER.'
              INTO ALERTA
              MOVE 'COBUSRLST.EXE' TO PROGID
              MOVE 'USRLST' TO PAGEX
              MOVE 'L' TO ACCTN
              INSPECT SYSCOMM REPLACING ALL ' ' BY '_'
              STRING '<script>'
                   'alert("'
                   FUNCTION TRIM (ALERTA)
                   '");'
                   newline
                   'window.location.href = "/cgi-bin/COBUSRLST.EXE?'
                   SYSCOMM
                   '";'
                   '</script>'
              INTO BUFFER
              DISPLAY BUFFER
              STOP RUN
           END-IF.

           IF PAGEACTIVE NOT = 'Y'
              PERFORM CREATEROWLOCK
              PERFORM SENDHTML
           ELSE
              CALL "CBL_GC_NANOSLEEP" USING "500000000"
              PERFORM GETPAGES
              MOVE 'L' TO ACCTN
              MOVE 0 TO IDET
              MOVE 'N' TO PAGEACTIVE
              MOVE 'USRLST' TO PAGEX
              MOVE 'COBUSRLST.EXE' TO PROGID
              INSPECT SYSCOMM REPLACING ALL " " BY "_"
              MOVE SPACES TO BUFFER
              STRING '<script>'
                   'alert("'
                   FUNCTION TRIM (ALERTA)
                   '");'
                   newline
                   'window.location.href = "/cgi-bin/COBUSRLST.EXE?'
                   SYSCOMM
                   '";'
                   '</script>'
              INTO BUFFER
              DISPLAY BUFFER
           END-IF. 
           STOP RUN.

       GETPAGES.
           PERFORM READPAGE.
      *     DISPLAY 'GETPAGES ' ACCTN, SYSCOMM END-DISPLAY.
           MOVE SPACES TO ALERTA.
           IF ACCTN = 'D' PERFORM DELETEUSR
              MOVE "USER WAS DELETED." TO ALERTA.
           IF ACCTN = 'U' PERFORM UPDATEUSR
              MOVE "UPDATE SUCCEDED, PASSWORD IS NOT UPDATED"
              TO ALERTA.
           IF ACCTN = 'C' PERFORM CREATEUSR
              MOVE "USER WAS SUCCESFULLY CREATED." TO ALERTA.

       DELETEUSR.
           MOVE SPACES TO BUFFER.
           STRING "DELETE FROM sysusers WHERE "
                "id = '", IDENT, "'"
                INTO BUFFER.
           MOVE 'DELETE USER' TO OPERACION.
           EXEC SQL 
              EXECUTE IMMEDIATE :BUFFER
           END-EXEC
           PERFORM SQLERRORES.
           
       UPDATEUSR.
      *     DISPLAY 'UPDATE ', IDET.
           MOVE SPACES TO BUFFER.
           STRING "UPDATE sysusers SET "
                "name = '" FUNCTION TRIM(NAME), "', "
                "email = '" FUNCTION TRIM(EMAIL), "', "
                "username = '" FUNCTION TRIM(USERNAME), "',"
                "comment = '" FUNCTION TRIM(COMMENT), "',"
                "agree = '" FUNCTION TRIM(AGREE), "',"
                "age = '" FUNCTION TRIM(AGE), "',"
                "type = '" FUNCTION TRIM(TIPO), "',"
                "timehms = '" FUNCTION TRIM(TIMEHMS), "',"
                "datedmy = '" FUNCTION TRIM(DATEDMY), "'"
                " WHERE id = '", IDENT , "'"
                INTO BUFFER.
           MOVE 'UPDATE USER' TO OPERACION.
      *     DISPLAY FUNCTION TRIM(BUFFER).
           EXEC SQL 
              EXECUTE IMMEDIATE :BUFFER
           END-EXEC
           PERFORM SQLERRORES.

           
       CREATEUSR.
      *     DISPLAY 'CREATE'.
           IF PWD NOT = RPWD
              MOVE 'L' TO ACCTN
              MOVE 0 TO IDET
              MOVE 'N' TO PAGEACTIVE
              MOVE 'USRLST' TO PAGEX
              MOVE 'COBUSRLST.EXE' TO PROGID
              INSPECT SYSCOMM REPLACING ALL " " BY "_"
              MOVE SPACES TO BUFFER
              MOVE 'PASSWORDS ARE DIFFERENT, USER CREATE CANCELLED'
                   TO ALERTA
              STRING '<script>'
                   'alert("'
                   FUNCTION TRIM (ALERTA)
                   '");'
                   '</script>'
                   '<a href="'
                   PROGID DELIMITED BY "_", 
                    "?", SYSCOMM,
                   '">Back to Users List.</a>'
              INTO BUFFER
              DISPLAY BUFFER
              STOP RUN
           ELSE
             MOVE SPACES TO BUFFER
             STRING "INSERT INTO sysusers VALUES (0,"
              "'" FUNCTION TRIM(NAME) "'," 
              "'" FUNCTION TRIM(EMAIL) "'," 
              "'" FUNCTION TRIM(USERNAME) "'," 
              "'" FUNCTION TRIM(PWD) "'," 
              "'" FUNCTION TRIM(DATEDMY) "'," 
              "'" FUNCTION TRIM(TIMEHMS) "'," 
              "'" FUNCTION TRIM(TIPO) "'," 
              "'" FUNCTION TRIM(AGE) "'," 
              "'" FUNCTION TRIM(AGREE) "'," 
              "'" FUNCTION TRIM(COMMENT) "')" 
                INTO BUFFER
             MOVE 'CREATE USER' TO OPERACION
      *      DISPLAY FUNCTION TRIM(BUFFER)
             EXEC SQL 
              EXECUTE IMMEDIATE :BUFFER
             END-EXEC
             PERFORM SQLERRORES
           END-IF.


       SENDHTML.
      *     DISPLAY 'SENDHTML'.
           IF ACCTN = 'C' OR ACCTN = ' '
              CALL "SENDPAGE" END-CALL
              MOVE PAGETOSEND TO PAGES
              PERFORM EMPTY-PAGE
              DISPLAY FUNCTION TRIM (PAGES).
           
           IF ACCTN = 'R' 
              CALL "SENDPAGE" END-CALL
              MOVE PAGETOSEND TO PAGES
              PERFORM FILL-PAGE
              DISPLAY FUNCTION TRIM (PAGES).

  
       FILL-PAGE.
      *       DISPLAY "FILLPAGE".
           MOVE FUNCTION NUMVAL (IDET) TO XNUM12
           MOVE 'READ SYSUSERS' TO OPERACION
           EXEC SQL 
              SELECT * from sysusers
               where id = :XNUM12
              into :IDENT, :NAME, :EMAIL, :USERNAME, :PWD,
                   :DATEDMY, :TIMEHMS, :TIPO, :AGE, :AGREE, :COMMENT
           END-EXEC
           PERFORM SQLERRORES
           MOVE FUNCTION SUBSTITUTE 
                (PAGES, '##id##', FUNCTION TRIM(IDENT)) TO PAGES
           MOVE FUNCTION SUBSTITUTE 
                (PAGES, '##name##', FUNCTION TRIM(name)) TO PAGES
           MOVE FUNCTION 
                SUBSTITUTE (PAGES, '##email##', FUNCTION TRIM(email)) 
                TO PAGES
           MOVE FUNCTION SUBSTITUTE 
                (PAGES, '##comment##', FUNCTION TRIM(comment))
                TO PAGES
           MOVE FUNCTION 
                SUBSTITUTE 
                (PAGES, '##username##', FUNCTION TRIM(username))
                TO PAGES
           MOVE FUNCTION SUBSTITUTE (PAGES, '##pwd##', '*****') 
                 TO PAGES
           MOVE FUNCTION SUBSTITUTE (PAGES, '##rpwd##', '*****') 
                 TO PAGES
           MOVE FUNCTION SUBSTITUTE (PAGES, '##datedmy##', datedmy) 
                 TO PAGES
           MOVE FUNCTION SUBSTITUTE (PAGES, '##timehms##', timehms) 
                 TO PAGES
           MOVE FUNCTION SUBSTITUTE (PAGES, '##type##', TIPO) TO PAGES

           IF age = '> 61'
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option5##', 'checked')
             TO PAGES
           ELSE
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option5##', ' ')
             TO PAGES
           END-IF
           IF age = '41-60'
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option4##', 'checked')
             TO PAGES
           ELSE 
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option4##', ' ')
             TO PAGES
           END-IF
           IF age = '31-40'
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option3##', 'checked')
             TO PAGES
           ELSE
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option3##', ' ')
             TO PAGES 
           END-IF
           IF age = '21-30'
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option2##', 'checked')
             TO PAGES
           ELSE 
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option2##', ' ')
             TO PAGES
           END-IF 
           IF age = '01-20'
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option1##', 'checked')
             TO PAGES
           ELSE 
             MOVE FUNCTION SUBSTITUTE (PAGES, '##option1##', ' ')
             TO PAGES
           END-IF 

           IF agree = 'Y' 
               MOVE FUNCTION SUBSTITUTE (PAGES, '##agree##', 'checked') 
               TO PAGES
               ELSE
               MOVE FUNCTION SUBSTITUTE (PAGES, '##agree##', ' ') 
               TO PAGES
           END-IF.
           MOVE FUNCTION SUBSTITUTE (PAGES, '##save##', 'disabled') 
                TO PAGES.
           MOVE FUNCTION SUBSTITUTE (PAGES, '##delete##', ' ') 
                TO PAGES.
           MOVE FUNCTION SUBSTITUTE (PAGES, '##update##', ' ') 
                TO PAGES.


       EMPTY-PAGE.
      *       DISPLAY "EMPTYPAGE".
            MOVE FUNCTION SUBSTITUTE (PAGES, '##id##"', '"') TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##name##"', '"') TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##email##"', '"') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '>##comment##<', '><') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##username##"', '"')
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##pwd##"', '"') TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##rpwd##"', '"') TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##datedmy##"', '"') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##timehms##"', '"') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##type##', ' ') TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##option1##', ' ') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##option2##', ' ') 
                  TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##option3##', ' ') 
                  TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##option4##', ' ') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##option5##', ' ') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##agree##"', '"') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##type##', ' ') TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##upda##', 'disabled') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##dele##', 'disabled') 
                 TO PAGES.
            MOVE FUNCTION SUBSTITUTE (PAGES, '##save##', ' ') 
                 TO PAGES.

         READPAGE.
           MOVE PAGEX TO XSQL16.
           MOVE SESSION TO SESIX.
           EXEC SQL
               DECLARE CUR_FLD CURSOR FOR
                   SELECT id, mapname, campo, 
                          lenght, comment, valor
                   FROM sysfields
                   WHERE mapname = :XSQL16
                   AND SESSION = :SESIX
                   ORDER BY id
           END-EXEC
           MOVE 'DECLARE CURSOR sysfield' TO OPERACION.
           PERFORM SQLERRORES.
           EXEC SQL 
              OPEN CUR_FLD
           END-EXEC
           MOVE 'POSICIONARSE ' TO OPERACION.
           PERFORM SQLERRORES.
           MOVE 0 TO INDIFIN.
           MOVE SPACES TO AGE.
           MOVE 'N' TO AGREE.
           IF RESULTADO NOT = SPACES MOVE 1 TO INDIFIN.
           PERFORM UNTIL INDIFIN = 1
              IF INDIFIN = 0
                 EXEC SQL 
                      FETCH CUR_FLD
                 INTO :IDFLD, :MAPFLD, :FLD, :LENFLD,
                      :COMFLD, :VALFLD
                 END-EXEC
                 MOVE 'LECTURA-NEXT MAPPAGE' TO OPERACION
                 PERFORM SQLERRORES
                 IF RESULTADO NOT = SPACES MOVE 1 TO INDIFIN
                 ELSE PERFORM MOVECAMPOS
                 END-IF 
               END-IF
           END-PERFORM.
           EXEC SQL
               CLOSE CUR_FLD
           END-EXEC
           MOVE 'CIERRE CURSOR ' TO OPERACION.
                 PERFORM SQLERRORES.

       MOVECAMPOS.
      *     DISPLAY FLD "-" VALFLD "<BR>".
           IF FLD = 'SYSCOMM' 
              MOVE VALFLD TO SYSCOMM2
              MOVE IDET2 TO IDET.
           IF FLD = 'id' MOVE VALFLD TO IDENT.
           IF FLD = 'agree' 
              IF VALFLD = 'on' 
                 MOVE 'Y' TO AGREE
              ELSE MOVE 'N' TO AGREE.
           IF FLD = 'type' MOVE VALFLD TO TIPO.
           IF FLD = 'timehms' MOVE VALFLD TO TIMEHMS.
           IF FLD = 'datedmy' MOVE VALFLD TO DATEDMY.
           IF FLD = 'pwd' MOVE VALFLD TO PWD.
           IF FLD = 'rpwd' MOVE VALFLD TO RPWD.
           IF FLD = 'username' MOVE VALFLD TO USERNAME.
           IF FLD = 'email' MOVE VALFLD TO EMAIL.
           IF FLD = 'name' MOVE VALFLD TO NAME.
           IF FLD = 'comment' MOVE VALFLD TO COMMENT.
           IF FLD = 'radio1' 
              IF VALFLD = 'on' MOVE '01-20' TO AGE.
           IF FLD = 'radio2' 
              IF VALFLD = 'on' MOVE '21-30' TO AGE.
           IF FLD = 'radio3' 
              IF VALFLD = 'on' MOVE '31-40' TO AGE.
           IF FLD = 'radio4' 
              IF VALFLD = 'on' MOVE '41-60' TO AGE.
           IF FLD = 'radio5' 
              IF VALFLD = 'on' MOVE '> 61' TO AGE.

        CHECKROWLOCK.
           MOVE SPACES TO BUFFER.
           MOVE 'READ SYSROWLOCK' TO OPERACION.
           MOVE USERN TO USER.
           MOVE IDET TO IDFLD.
           MOVE SPACES TO STARWL, USRRWL, SESRWL.
           MOVE SPACES TO RESULTADO.
           EXEC SQL 
              SELECT * FROM sysrowlock
                WHERE user = :USER
                AND rowid = :IDFLD
                AND tabla = :TABLA
              INTO :IDRWL, :TABRWL, :RWRWL, :USRRWL, :SESRWL, :STARWL
           END-EXEC
           PERFORM SQLERRORES.
      *     DISPLAY "CHECKROWLOCK:", USER, IDFLD, TABLA STARWL.

        CREATEROWLOCK.
           MOVE SPACES TO BUFFER.
           STRING "INSERT INTO sysrowlock VALUES (0,'"
               FUNCTION TRIM(TABLA), "','"
               IDET, "','"
               FUNCTION TRIM(USERN), "','"
               FUNCTION TRIM(SESSION), "','"
               FUNCTION FORMATTED-CURRENT-DATE("YYYYMMDDThhmmss.ss"), 
               "');"
           INTO BUFFER.
           MOVE 'CREATE ROWLOCK' TO OPERACION.
           EXEC SQL 
             EXECUTE IMMEDIATE :BUFFER
           END-EXEC
           PERFORM SQLERRORES.
      *     DISPLAY "CREATEROWLOCK:", USERN, IDET, TABLA SESSION.

        SQLERRORES.
           MOVE SPACES TO RESULTADO.
           IF SQLCODE < 0 
                      DISPLAY OPERACION,  '*SQLSTATE='  SQLSTATE,
                              ', *SQLCODE=' SQLCODE
              IF SQLERRML > 0
                 DISPLAY 'SQL Error message:' SQLERRMC(1:SQLERRML)
                 MOVE  SQLERRMC(1:SQLERRML) 
                       TO RESULTADO
              END-IF
              MOVE SQLCODE TO RETURN-CODE
              STOP RUN                                                           
           ELSE IF SQLCODE > 0 AND NOT = 100
                      DISPLAY OPERACION, 'SQLSTATE='  SQLSTATE,
                              ', SQLCODE=' SQLCODE
              IF SQLERRML > 0
                 DISPLAY 'SQL Warning message:' SQLERRMC(1:SQLERRML)
                 MOVE  SQLERRMC(1:SQLERRML)
                       TO RESULTADO
              END-IF
           END-IF.
           IF SQLCODE = 100
              MOVE 'FIN DE TABLA' TO RESULTADO
           END-IF.
           MOVE 'COMMIT;' TO BUFFER.
           EXEC SQL 
             EXECUTE IMMEDIATE :BUFFER
           END-EXEC
           .
       END PROGRAM COBNEWUSR.
