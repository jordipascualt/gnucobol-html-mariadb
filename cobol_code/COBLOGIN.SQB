      *> ***************************************************************
      *> Author:    Jordi Pascual
      *> About:     GNUCobol + HTML + MariaDB (Mysql).
      *> Date:      dec-2024
      *> License:   Public Domain 
      *> Usage:     Login control.
      *> https://github.com/jordipascualt/gnucobol-html-mariadb
      *> ***************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. COBLOGIN.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

       DATA DIVISION.
       FILE SECTION.

       WORKING-STORAGE SECTION.
       EXEC SQL 
           BEGIN DECLARE SECTION 
       END-EXEC.
       01  HOSTVARS.
           05 BUFFER               PIC X(512).
           05 XSQL1                PIC X.
           05 XSQL16               PIC X(16).
           05 XSQL32               PIC X(32).
           05 XNUM12               PIC 9(12).
           05 HCAMPO               PIC X(32).
           05 PWDFLD               PIC X(32).
           05 PAGEH                PIC X(63535).
       01  USERS.
           05 COMMENT              PIC X(128).
           05 AGREE                PIC X.
           05 AGE                  PIC X(16).
           05 TIPO                 PIC X(16).
           05 TIMEHMS              PIC X(8).
           05 DATEDMY              PIC X(10).
           05 PWD                  PIC X(32).
           05 USERNAME             PIC X(32).
           05 EMAIL                PIC X(32).
           05 IDENT                PIC X(11).
           05 NAME                 PIC X(64).
       01  SYSFLD.
           05 IDFLD                PIC 9(11).
           05 TNXFLD               PIC 9(11).
           05 MAPFLD               PIC X(16).
           05 FLD                  PIC X(32).
           05 LENFLD               PIC 9(11).
           05 COMFLD               PIC X(128).
           05 VALFLD               PIC X(128).
       01  SYSCOMM.
           05 ACCTN                PIC X.
           05 PAGEX                PIC X(16).
           05 IDET                 PIC X(11).
           05 PAGEACTIVE           PIC X.
           05 PROGID               PIC X(16).
           05 USER                 PIC X(32).
           05 SESSION              PIC X(32).
       EXEC SQL
           END DECLARE SECTION 
       END-EXEC.
       01  RESULTADO               PIC X(256).
       01  OPERACION               PIC X(50).
       01  IN-STATUS               PIC 99.
       01  NEWLINE                 PIC X     VALUE X'0A'.

       01  INDIFIN                 PIC 9 VALUE 0.
       01  IX                      PIC 9999 VALUE 0.
       01  IT                      PIC 99 VALUE 0.
       01  DATETIME                PIC X(21).
       01  XSTAMP                  PIC X(19).
       01  SES                     PIC X(32).
       01  LASTTNX                 PIC 9(11).
       01  BUTTONCK                PIC X(128).
       01  PAGEA                   PIC X(63535).
 
       01  SYSCOMMALL              EXTERNAL.
           05 SYSCOMMX.
             07 ACCTNX             PIC X.
             07 PAGEXX             PIC X(16).
             07 IDETX              PIC X(11).
             07 PAGEACTIVEX        PIC X.
             07 PROGIDX            PIC X(16).
             07 USERNX             PIC X(32).
             07 SESSIONX           PIC X(32).
           05 LIMITE               PIC 9(10).
           05 SALTAR               PIC 9(10).
           05 BUSCAR               PIC X(32).
           05 PAGETOSEND           PIC X(63535).

       PROCEDURE DIVISION.
       COBLOGIN.
      *> ALWAYS SEND OUT THE CONTENT-TYPE BEFORE ANY OTHER IO
           DISPLAY
             "Content-type: text/html"
             NEWLINE
           END-DISPLAY
           MOVE SPACES TO SYSCOMMALL.
           MOVE 0 TO LIMITE, SALTAR.
           ACCEPT SYSCOMM FROM COMMAND-LINE.
           INSPECT SYSCOMM REPLACING ALL "_" BY " ".
           MOVE SYSCOMM TO SYSCOMMX.
           IF ACCTN < '*' 
              MOVE 'C' TO ACCTN
              MOVE 'LOGIN' TO PAGEX
              MOVE 0 TO IDET
              MOVE 'N' TO PAGEACTIVE
              MOVE 'COBLOGIN.EXE' TO PROGID.
           MOVE 'CONNECT' TO OPERACION.
           STRING 'DRIVER={MariaDB};'
                'SERVER=localhost;'
                'PORT=3306;'
                'DATABASE=cobolhtml;'
                'USER=*****;'
                'PASSWORD=*****;'
           INTO BUFFER.
           EXEC SQL 
             CONNECT TO :BUFFER 
           END-EXEC
           PERFORM SQLERRORES.
           IF PAGEACTIVE = 'N'
              PERFORM TOSENDPAGE
           ELSE PERFORM TOGETPAGE.
           STOP RUN.

       TOSENDPAGE.
           INSPECT SYSCOMM REPLACING ALL " " BY "_".
           MOVE 'Y' TO PAGEACTIVE.
           MOVE SYSCOMM TO SYSCOMMX.
           MOVE "CALL SENDPAGE" TO PAGETOSEND.
           CALL "SENDPAGE".
           DISPLAY FUNCTION TRIM(PAGETOSEND).

       TOGETPAGE.
           CALL "CBL_GC_NANOSLEEP" USING "500000000".
           MOVE PAGEX TO XSQL16.
           EXEC SQL
               DECLARE CUR_FLD CURSOR FOR
                   SELECT CAMPO, VALOR FROM sysfields
                   WHERE MAPNAME = :XSQL16
           END-EXEC
           MOVE 'DECLARE CURSOR SYSFIELDS' TO OPERACION.
           PERFORM SQLERRORES.
           EXEC SQL 
              OPEN CUR_FLD
           END-EXEC
           MOVE 'POSICIONARSE ' TO OPERACION.
           PERFORM SQLERRORES.
           IF RESULTADO NOT = SPACES MOVE 1 TO INDIFIN.
           PERFORM UNTIL INDIFIN = 1
              IF INDIFIN = 0
                 EXEC SQL 
                      FETCH CUR_FLD
                 INTO 
                      :FLD, :VALFLD
                 END-EXEC
                 MOVE 'LECTURA-NEXT FLD' TO OPERACION
                 PERFORM SQLERRORES
                 IF RESULTADO NOT = SPACES MOVE 1 TO INDIFIN
                 ELSE ADD 1 TO IX
                    IF LASTTNX = 0
                       MOVE TNXFLD TO LASTTNX
                    END-IF
                    IF TNXFLD NOT = LASTTNX
                       MOVE 1 TO INDIFIN
                    ELSE
                       PERFORM VALORES
                    END-IF
                 END-IF
           END-PERFORM.
           EXEC SQL
               CLOSE CUR_FLD
           END-EXEC
           MOVE 'CIERRE CURSOR ' TO OPERACION.
           PERFORM SQLERRORES.
           MOVE 'C' TO ACCTN.

      * PROGRAM LOGIC. PAGE READ. **********************************

         MOVE 'READ USER' TO OPERACION.
         EXEC SQL 
              SELECT * FROM sysusers
               WHERE USERNAME = :USER
               INTO :IDENT, :NAME, :EMAIL, :USERNAME, :PWD,
                      :DATEDMY, :TIMEHMS, :TIPO, :AGE, :AGREE, 
                      :COMMENT
         END-EXEC
         PERFORM SQLERRORES.

      *   FOR DEBUGG.
      *   DISPLAY USER "-" USERNAME "-" PWDFLD, "-" PWD "-" RESULTADO.
      *   STOP RUN.

         IF PWD = PWDFLD AND RESULTADO = SPACES
            PERFORM DELETEOLDERS
            MOVE 'C' TO ACCTN
            MOVE 0 TO IDET
            MOVE 'N' TO PAGEACTIVE
            MOVE 'MENU' TO PAGEX
            MOVE 'COBOLMENU.EXE' TO PROGID
            INSPECT SYSCOMM REPLACING ALL " " BY "_"
            STRING PROGID DELIMITED BY "_", 
               "?", SYSCOMM 
               INTO RESULTADO   
            STRING '<script>'
                   'document.cookie = "USERNAME='
                   FUNCTION TRIM(USERNAME)
                   ' ";'
                   'window.location.href = "'
                   RESULTADO
                   '";'
                   '</script>'
              INTO BUFFER
            DISPLAY BUFFER
            STOP RUN
         ELSE
            PERFORM DELETEOLDERS
            MOVE SPACES TO BUFFER
            MOVE 'C' TO ACCTN
            MOVE 0 TO IDET
            MOVE 'N' TO PAGEACTIVE
            MOVE 'LOGIN' TO PAGEX
            MOVE 'COBLOGIN.EXE' TO PROGID
            INSPECT SYSCOMM REPLACING ALL " " BY "_"
            STRING PROGID DELIMITED BY "_", 
               "?", SYSCOMM 
               INTO RESULTADO
            MOVE 'N' TO PAGEACTIVE
            INSPECT SYSCOMM REPLACING ALL " " BY "_"
            STRING '<script>'
                   'alert("USER OR PASSWORD NOT OK");'
                   'window.location.href = "'
                    RESULTADO
                   '";'
                   '</script>'
              INTO BUFFER
            DISPLAY BUFFER.
         STOP RUN.
       DELETEOLDERS.
         MOVE 'DELETE OLD FIELDS/USER' TO OPERACION
         MOVE SPACES TO BUFFER
         STRING "DELETE FROM sysfields WHERE user = '"
                   FUNCTION TRIM (USER) 
                   "' or user < '*'"
                   " or user = 'null'"
                   " or user = 'nouser';"
         INTO BUFFER
         EXEC SQL 
               EXECUTE IMMEDIATE :BUFFER
         END-EXEC.
         PERFORM SQLERRORES.
         MOVE SPACES TO BUFFER.
         STRING "DELETE FROM sysrowlock WHERE "
               "user = '"
               FUNCTION TRIM(USER)
                 "'"
         INTO BUFFER.
         MOVE 'DELETE ROWLOCK' TO OPERACION.
         EXEC SQL 
            EXECUTE IMMEDIATE :BUFFER
         END-EXEC
         PERFORM SQLERRORES. 

       VALORES.
      *     DISPLAY FLD , "-", VALFLD, "-<BR>".
           IF FLD = 'SYSCOMM' MOVE VALFLD TO SYSCOMM.
           IF FLD = 'buttonck' MOVE VALFLD TO BUTTONCK.
           IF FLD = 'password' MOVE VALFLD TO PWDFLD.
           IF FLD = 'usuario' MOVE VALFLD TO USER.

        SQLERRORES.
           MOVE SPACES TO RESULTADO.
           IF SQLCODE < 0 
                      DISPLAY PROGID, '-', OPERACION, 
                             ' *SQLSTATE='  SQLSTATE,
                             ' *SQLCODE=' SQLCODE
              IF SQLERRML > 0
                 DISPLAY 'SQL ERROR MESSAGE:' SQLERRMC(1:SQLERRML)
                 MOVE  SQLERRMC(1:SQLERRML) 
                       TO RESULTADO
              END-IF
              MOVE SQLCODE TO RETURN-CODE
              STOP RUN                                                           
           ELSE IF SQLCODE > 0 AND NOT = 100
                      DISPLAY 'SQLSTATE='  SQLSTATE,
                              ', SQLCODE=' SQLCODE
              IF SQLERRML > 0
                 DISPLAY 'SQL WARNING MESSAGE:' SQLERRMC(1:SQLERRML)
                 MOVE  SQLERRMC(1:SQLERRML)
                       TO RESULTADO
              END-IF
           END-IF.
           IF SQLCODE = 100
              MOVE 'FIN DE TABLA' TO RESULTADO
           END-IF.
           MOVE 'COMMIT' TO BUFFER.
           EXEC SQL 
             EXECUTE IMMEDIATE :BUFFER
           END-EXEC
           .
       END PROGRAM COBLOGIN.

