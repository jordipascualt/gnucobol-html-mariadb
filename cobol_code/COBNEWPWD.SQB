      *> ***************************************************************
      *> Author:    Jordi Pascual
      *> About:     GNUCobol + HTML + MariaDB (Mysql).
      *> Date:      dec-2024
      *> License:   Public Domain 
      *> Usage:     Create a new password.
      *> https://github.com/jordipascualt/gnucobol-html-mariadb
      *> ***************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. COBNEWPWD.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       DATA DIVISION.
       FILE SECTION.
       WORKING-STORAGE SECTION.
       EXEC SQL 
         BEGIN DECLARE SECTION 
       END-EXEC
      *> ***************************************************************
      *> HTML / SQL NEEDED FIELDS
      *> ***************************************************************
       01  HOSTVARS.
           05 BUFFER               PIC X(1024).
           05 XSQL16               PIC X(16).
           05 XSQL32               PIC X(32).
           05 XSESI                PIC X(32).
           05 HCAMPO               PIC X(32).
           05 USER                 PIC X(32).
       01  SYSFLD.
           05 IDFLD                PIC 9(11).
           05 MAPFLD               PIC X(16).
           05 FLD                  PIC X(32).
           05 LENFLD               PIC 9(11).
           05 COMFLD               PIC X(128).
           05 VALFLD               PIC X(128).
      *> ***************************************************************
      *> ROW FOR TABLE FIELDS
      *> ***************************************************************
       01  USERS.
           05 COMMENT              PIC X(128).
           05 AGREE                PIC X.
           05 AGE                  PIC X(16).
           05 TIPO                 PIC X(16).
           05 TIMEHMS              PIC X(8).
           05 DATEDMY              PIC X(10).
           05 PWD                  PIC X(32).
           05 RPWD                 PIC X(32).
           05 USERNAME             PIC X(32).
           05 EMAIL                PIC X(32).
           05 IDENT                PIC X(11).
           05 NAME                 PIC X(64).
       EXEC SQL
          END DECLARE SECTION 
       END-EXEC.
      *> ***************************************************************
      *> WORKING NEEDED FIELDS
      *> ***************************************************************
       01  RESULTADO               PIC X(256).
       01  OPERACION               PIC X(50).
       01  ALERTA                  PIC X(128).
       01  NEWLINE                 PIC X VALUE X'0a'.
       01  INDIFIN                 PIC 9 VALUE 0.
       01  BUTTONCK                PIC X(128).
       01  PAGES                   PIC X(65365).

       01  SYSCOMMALL              EXTERNAL.
           05 SYSCOMM.
             07 ACCTN              PIC X.
             07 PAGEX              PIC X(16).
             07 IDET               PIC X(11).
             07 PAGEACTIVE         PIC X.
             07 PROGID             PIC X(16).
             07 USERN              PIC X(32).
             07 SESSION            PIC X(32).
           05 LIMITE               PIC 9(10).
           05 SALTAR               PIC 9(10).
           05 BUSCAR               PIC X(32).
           05 PAGETOSEND           PIC X(63535).
     
       PROCEDURE DIVISION.
       COBNEWPWD.
      *> ***************************************************************
      *> INITIALIZATION
      *> Always send out the Content-type before any other IO
      *> INITIALIZE SYSCOMM FOR NO COMMAND LINE CONTENT
      *> ***************************************************************
       DISPLAY
           "Content-type: text/html"
           NEWLINE
       END-DISPLAY

         MOVE SPACES TO SYSCOMM.
         ACCEPT SYSCOMM FROM COMMAND-LINE.
      *   DISPLAY SYSCOMM.
         INSPECT SYSCOMM REPLACING ALL "_" BY " ".
         IF ACCTN < '*' 
              MOVE 'C' TO ACCTN
              MOVE 'NWPWD' TO PAGEX
              MOVE 0 TO IDET
              MOVE 'N' TO PAGEACTIVE
              MOVE 'COBNEWPWD.EXE' TO PROGID.
      *> ***************************************************************
      *> DATABSE CONNECT.
      *> ***************************************************************
           STRING 'DRIVER={MariaDB};'
                'SERVER=localhost;'
                'PORT=3306;'
                'DATABASE=cobolhtml;'
                'USER=*****;'
                'PASSWORD=*****;'
           INTO BUFFER.
           EXEC SQL 
               CONNECT TO :BUFFER 
           END-EXEC
           PERFORM SQLERRORES.
      *> ***************************************************************
      *> PROGRAM HTML LOGIC
      *> ***************************************************************
           IF PAGEACTIVE NOT = 'Y'
                PERFORM SENDHTML
           ELSE
              PERFORM GETPAGES
              MOVE 'C' TO ACCTN
              MOVE 0 TO IDET
              MOVE 'N' TO PAGEACTIVE
              MOVE 'LOGIN' TO PAGEX
              MOVE 'COBLOGIN.EXE' TO PROGID
              INSPECT SYSCOMM REPLACING ALL " " BY "_"
              MOVE SPACES TO BUFFER
              STRING '<script>'
                   'alert("'
                   FUNCTION TRIM (ALERTA)
                   '");'
                   NEWLINE
                   'window.location.href = "/cgi-bin/COBLOGIN.EXE?'
                   SYSCOMM
                   '";'
                   '</script>'
              INTO BUFFER
            DISPLAY BUFFER
           END-IF. 
           STOP RUN.
      *> ***************************************************************
      *> READ PAGES DATA
      *> ***************************************************************
       GETPAGES.
           PERFORM READPAGE.
           MOVE SPACES TO ALERTA.
           PERFORM CHANGEPWD
           MOVE "PASSWORD WAS SUCCESFULLY CHANGED." TO ALERTA.

      *> ***************************************************************
      *> PROGRAM LOGIC HTML DATA LOADED
      *> ***************************************************************
       CHANGEPWD.
           MOVE USERNAME TO XSQL32.
           MOVE "CHECK EMAIL" TO OPERACION.
           EXEC SQL
           SELECT email FROM sysusers 
                  WHERE username = :XSQL32
                  INTO :HCAMPO
           END-EXEC
           IF HCAMPO NOT = EMAIL
              MOVE 'C' TO ACCTN
              MOVE 0 TO IDET
              MOVE 'N' TO PAGEACTIVE
              MOVE 'LOGIN' TO PAGEX
              MOVE SPACES TO BUFFER
              STRING 'EMAIL IS NOT THE SAME PREVIOUSLY REGISTERED '
                     ' OR USER NOT EXIST. '
                   INTO ALERTA
              MOVE 'COBLOGIN.EXE' TO PROGID
              INSPECT SYSCOMM REPLACING ALL " " BY "_"
              MOVE SPACES TO BUFFER
              STRING '<script>'
                   'alert("'
                   FUNCTION TRIM (ALERTA)
                   '");'
                   NEWLINE
                   'window.location.href = "/cgi-bin/COBLOGIN.EXE?'
                   SYSCOMM
                   '";'
                   '</script>'
              INTO BUFFER
              DISPLAY BUFFER
              STOP RUN
           END-IF.           
           IF PWD NOT = RPWD 
              MOVE 'C' TO ACCTN
              MOVE 0 TO IDET
              MOVE 'N' TO PAGEACTIVE
              MOVE 'LOGIN' TO PAGEX
              MOVE SPACES TO BUFFER
              MOVE SPACES TO ALERTA
              STRING 'PASSWORDS ARE DIFFERENT, '
                   'CHANGE PASSWORD CANCELLED. '
      *             PWD '-' RPWD '-' USERNAME
                   INTO ALERTA
              MOVE 'COBLOGIN.EXE' TO PROGID
              INSPECT SYSCOMM REPLACING ALL " " BY "_"
              MOVE SPACES TO BUFFER
              STRING '<script>'
                   'alert("'
                   FUNCTION TRIM (ALERTA)
                   '");'
                   NEWLINE
                   'window.location.href = "/cgi-bin/COBLOGIN.EXE?'
                   SYSCOMM
                   '";'
                   '</script>'
              INTO BUFFER
              DISPLAY BUFFER
              STOP RUN
           ELSE
             MOVE SPACES TO BUFFER
             STRING "UPDATE sysusers SET pwd='"
                    PWD, "'"
                    " WHERE username = '"
                    USERNAME
                    "'"
                INTO BUFFER
             MOVE 'CHANGE PWD' TO OPERACION
             EXEC SQL 
              EXECUTE IMMEDIATE :BUFFER
             END-EXEC
             PERFORM SQLERRORES
           END-IF.
      *> ***************************************************************
      *> CALL TO BUILD AND SEND HTML PAGE
      *> ***************************************************************
       SENDHTML.
            IF ACCTN = 'C' OR ACCTN = ' '
              CALL "SENDPAGE" END-CALL
              MOVE PAGETOSEND TO PAGES
              PERFORM EMPTY-PAGE
              DISPLAY FUNCTION TRIM (PAGES).
      *> ***************************************************************
      *> FILL HTML PAGE FIELDS AS INITIAL EMPTY VALUES
      *> ***************************************************************
        EMPTY-PAGE.
      *       DISPLAY "EMPTYPAGE".
            MOVE FUNCTION SUBSTITUTE (PAGES, '##name##"', '"') TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##email##"', '"') 
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##username##"', '"')
                 TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##pwd##"', '"') TO PAGES
            MOVE FUNCTION SUBSTITUTE (PAGES, '##rpwd##"', '"') TO PAGES.

      *> ***************************************************************
      *> READ HTML PAGE FIELDS
      *> ***************************************************************
         READPAGE.
           CALL "CBL_GC_NANOSLEEP" USING "500000000".
           MOVE PAGEX TO XSQL16.
           MOVE USERN TO USER.
           MOVE SESSION TO XSESI.
           EXEC SQL
               DECLARE CUR_FLD CURSOR FOR
                   SELECT id, mapname, campo, 
                          lenght, comment, valor
                   FROM sysfields
                   WHERE mapname = :XSQL16
                   AND user = :USER
                   AND session = :XSESI
                   ORDER BY id
           END-EXEC
           MOVE 'DECLARE CURSOR sysfield' TO OPERACION.
           PERFORM SQLERRORES.
           EXEC SQL 
              OPEN CUR_FLD
           END-EXEC
           MOVE 'POSICIONARSE ' TO OPERACION.
           PERFORM SQLERRORES.
           MOVE 0 TO INDIFIN.
           MOVE SPACES TO AGE.
           MOVE 'N' TO AGREE.
           IF RESULTADO NOT = SPACES MOVE 1 TO INDIFIN.
           PERFORM UNTIL INDIFIN = 1
              IF INDIFIN = 0
                 EXEC SQL 
                      FETCH CUR_FLD
                 INTO :IDFLD, :MAPFLD, :FLD, :LENFLD,
                      :COMFLD, :VALFLD
                 END-EXEC
                 MOVE 'LECTURA-NEXT MAPPAGE' TO OPERACION
                 PERFORM SQLERRORES
                 IF RESULTADO NOT = SPACES MOVE 1 TO INDIFIN
                 ELSE PERFORM MOVECAMPOS
                 END-IF 
               END-IF
           END-PERFORM.
           EXEC SQL
               CLOSE CUR_FLD
           END-EXEC
           MOVE 'CIERRE CURSOR ' TO OPERACION.
                 PERFORM SQLERRORES.
           MOVE SPACES TO BUFFER.
           MOVE 'DELETE USED FIELDS' TO OPERACION.
           STRING 'DELETE FROM sysfields WHERE '
                   "mapname = '" XSQL16 
                   "' AND user = '" USER
                   "' AND session = '" XSESI
                   "';"
              INTO BUFFER.
           EXEC SQL 
                EXECUTE IMMEDIATE  :BUFFER
           END-EXEC
           PERFORM SQLERRORES.
                  
      *> ***************************************************************
      *> MOVE HTML DATE TO ROW FIELDS
      *> ***************************************************************
       MOVECAMPOS.
      *     DISPLAY FLD "-" VALFLD "<BR>".
           IF FLD = 'pwd' MOVE VALFLD TO PWD.
           IF FLD = 'rpwd' MOVE VALFLD TO RPWD.
           IF FLD = 'username' MOVE VALFLD TO USERNAME.
           IF FLD = 'email' MOVE VALFLD TO EMAIL.
      *> ***************************************************************
      *> SQL ERROR 
      *> ***************************************************************
        SQLERRORES.
           MOVE SPACES TO RESULTADO.
           IF SQLCODE < 0 
                      DISPLAY OPERACION,  '*SQLSTATE='  SQLSTATE,
                              ', *SQLCODE=' SQLCODE
              IF SQLERRML > 0
                 DISPLAY 'SQL Error message:' SQLERRMC(1:SQLERRML)
                 MOVE  SQLERRMC(1:SQLERRML) 
                       TO RESULTADO
              END-IF
              MOVE SQLCODE TO RETURN-CODE
              STOP RUN                                                           
           ELSE IF SQLCODE > 0 AND NOT = 100
                      DISPLAY 'SQLSTATE='  SQLSTATE,
                              ', SQLCODE=' SQLCODE
              IF SQLERRML > 0
                 DISPLAY 'SQL Warning message:' SQLERRMC(1:SQLERRML)
                 MOVE  SQLERRMC(1:SQLERRML)
                       TO RESULTADO
              END-IF
           END-IF.
           IF SQLCODE = 100
              MOVE 'END OF ROWS' TO RESULTADO
           END-IF.
           MOVE 'COMMIT' TO BUFFER.
           EXEC SQL 
             EXECUTE IMMEDIATE :BUFFER
           END-EXEC
           .
       END PROGRAM COBNEWPWD.
