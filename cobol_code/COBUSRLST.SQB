      *> ***************************************************************
      *> Author:    Jordi Pascual
      *> About:     GNUCobol + HTML + MariaDB (Mysql).
      *> Date:      dec-2024
      *> License:   Public Domain 
      *> Usage:     Users List and edit Options.
      *> https://github.com/jordipascualt/gnucobol-html-mariadb
      *> ***************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. COBUSRLST.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

       DATA DIVISION.
       FILE SECTION.

       WORKING-STORAGE SECTION.
       EXEC SQL 
           BEGIN DECLARE SECTION 
       END-EXEC.
       01  HOSTVARS.
           05 BUFFER               PIC X(1024).
           05 PAGEH                PIC X(65535).
           05 XSQL1                PIC X.
           05 XSQL16               PIC X(16).
           05 XNUM12               PIC 9(12).
           05 NROWS                PIC 9(12).
           05 HCAMPO               PIC X(32).
           05 XSQL32               PIC X(32).
           05 SESID                PIC X(32).
           05 LIMITE               PIC 9(10).
           05 SALTAR               PIC 9(10).
           05 FILLPRG              PIC X(16).
       01  SYSFLD.
           05 IDFLD                PIC 9(11).
           05 TNXFLD               PIC 9(11).
           05 MAPFLD               PIC X(16).
           05 FLD                  PIC X(32).
           05 LENFLD               PIC 9(11).
           05 COMFLD               PIC X(128).
           05 VALFLD               PIC X(128).
       01  SYSCOMM.
           05 ACCTN                PIC X.
           05 PAGEX                PIC X(16).
           05 IDET                 PIC X(11).
           05 PAGEACTIVE           PIC X.
           05 PROGID               PIC X(16).
           05 USERSYS              PIC X(32).
           05 SESSYS               PIC X(32).
       EXEC SQL
           END DECLARE SECTION 
       END-EXEC.
       01  RESULTADO               PIC X(256).
       01  OPERACION               PIC X(50).
       01  NEWCODI                 PIC 9(12).
       01  BUTTONCK                PIC X(16).

       01  TABLA.
           05 ELEM                 PIC X(512) OCCURS 15.
       01 IN-STATUS                PIC 99.
       01 NEWLINE                  PIC X      VALUE X'0A'.
 
       01  INDIFIN                 PIC 9 VALUE 0.
       01  IX                      PIC 9999 VALUE 0.
       01  IT                      PIC 99 VALUE 0.
       01  DATETIME                PIC X(21).
       01  XSTAMP                  PIC X(19).
       01  PAGEA                   PIC X(63535).
       01  PAGES                   PIC X(65365).
       01  PAGET                   PIC X(65365).
       01  ZLIMITE                 PIC ZZZZZZZZZ9.
       01  ZSALTAR                 PIC ZZZZZZZZZ9.
       01  ZCODE                   PIC ZZZZZZZZZ9.
 
       01  SYSCOMMALL              EXTERNAL.
           05 SYSCOMMX.
             07 ACCTNX             PIC X.
             07 PAGEXX             PIC X(16).
             07 IDETX              PIC X(11).
             07 PAGEACTIVEX        PIC X.
             07 PROGIDX            PIC X(16).
             07 USERN              PIC X(32).
             07 SESSION            PIC X(32).
           05 LIMITEX              PIC 9(10).
           05 SALTARX              PIC 9(10).
           05 BUSCAR               PIC X(32).
           05 PAGETOSENDX          PIC X(63535).

      *  DD-MM-YYYY HH:MM:SS
      *  1234567890123456789

      *> ***************************************************************
       PROCEDURE DIVISION.
       COBUSRLST.
      *> ALWAYS SEND OUT THE CONTENT-TYPE BEFORE ANY OTHER IO
       DISPLAY
             "Content-type: text/html"
           NEWLINE
       END-DISPLAY
       MOVE SPACES TO SYSCOMMALL.
       ACCEPT SYSCOMM FROM COMMAND-LINE.
         INSPECT SYSCOMM REPLACING ALL "_" BY " ".
         IF ACCTN < '*' 
            MOVE 'C' TO ACCTN
            MOVE 'USRLST' TO PAGEX
            MOVE 0 TO IDET
            MOVE 'N' TO PAGEACTIVE
            MOVE 'COBUSRLST.EXE' TO PROGID.
           MOVE SYSCOMM TO SYSCOMMX.
           STRING 'DRIVER={MariaDB};'
                'SERVER=localhost;'
                'PORT=3306;'
                'DATABASE=cobolhtml;'
                'USER=*****;'
                'PASSWORD=*****;'
           INTO BUFFER
         EXEC SQL 
           CONNECT TO :BUFFER 
         END-EXEC
         PERFORM SQLERRORES.
           MOVE SPACES TO BUFFER.
           MOVE "NUM ROWS" TO OPERACION.
           EXEC SQL 
              select count(*) from sysusers
              into :NROWS
           END-EXEC
           PERFORM SQLERRORES.
         IF PAGEACTIVE = 'N'
            MOVE 0 TO SALTAR
            MOVE 14 TO LIMITE
            MOVE SPACES TO BUSCAR
            PERFORM SENDPAGE
            PERFORM FILLPAGE
         ELSE 
           CALL "CBL_GC_NANOSLEEP" USING "500000000"
            PERFORM READPAGE
            MOVE 14 TO LIMITE
            IF BUTTONCK = "back" 
               IF SALTAR NOT < LIMITE
                  SUBTRACT LIMITE FROM SALTAR
               ELSE
                  MOVE 0 TO SALTAR
               END-IF
            END-IF   
            IF BUTTONCK = "forw" 
               ADD LIMITE TO SALTAR
               IF SALTAR > NROWS
                  MOVE NROWS TO SALTAR
                  SUBTRACT 1 FROM SALTAR
               END-IF
            END-IF
            IF BUTTONCK NOT = "search" 
               MOVE SPACES TO BUSCAR
            END-IF
            PERFORM SENDPAGE
            PERFORM FILLPAGE
         END-IF.
         GOBACK.
       SENDPAGE.
           MOVE "CALL SENDPAGE" TO PAGETOSENDX.
           MOVE SALTAR TO SALTARX.
           MOVE LIMITE TO LIMITEX.
      *    DISPLAY LIMITEX, '-', SALTARX, '-', BUSCAR, '*'
           CALL 'SENDPAGE'.
           MOVE PAGETOSENDX TO PAGEH.
       READPAGE.
           MOVE PAGEX TO XSQL16.
           EXEC SQL
               DECLARE CUR_FLD CURSOR FOR
                   SELECT ID, MAPNAME, CAMPO, 
                          LENGHT, COMMENT, VALOR
                   FROM sysfields
                   WHERE MAPNAME = :XSQL16
                   ORDER BY ID
           END-EXEC
           MOVE 'DECLARE CURSOR SYSFIELDS' TO OPERACION.
           PERFORM SQLERRORES.
           EXEC SQL 
              OPEN CUR_FLD
           END-EXEC
           MOVE 'POSICIONARSE ' TO OPERACION.
           PERFORM SQLERRORES.
           MOVE 0 TO INDIFIN.
           MOVE SPACES TO BUSCAR.
           IF RESULTADO NOT = SPACES MOVE 1 TO INDIFIN.
           PERFORM UNTIL INDIFIN = 1
              IF INDIFIN = 0
                 EXEC SQL 
                      FETCH CUR_FLD
                 INTO :IDFLD, :MAPFLD, :FLD, :LENFLD,
                      :COMFLD, :VALFLD
                 END-EXEC
                 MOVE 'LECTURA-NEXT MAPPAGE' TO OPERACION
                 PERFORM SQLERRORES
                 IF RESULTADO NOT = SPACES MOVE 1 TO INDIFIN
                 ELSE PERFORM MOVECAMPOS
                 END-IF 
               END-IF
           END-PERFORM.
           EXEC SQL
               CLOSE CUR_FLD
           END-EXEC
           MOVE 'CIERRE CURSOR ' TO OPERACION.
                 PERFORM SQLERRORES.

       MOVECAMPOS.
      *     DISPLAY FLD "-" VALFLD "<BR>".
           IF FLD = 'busca' MOVE VALFLD TO BUSCAR. 
           IF FLD = 'limite' MOVE VALFLD TO LIMITE.
           IF FLD = 'saltar' MOVE VALFLD TO SALTAR
              SUBTRACT 1 FROM SALTAR.
           IF FLD = 'buttonck' MOVE VALFLD TO BUTTONCK. 

       FILLPAGE.
         ADD 1 TO SALTAR.
         MOVE SALTAR TO ZSALTAR.
         SUBTRACT 1 FROM SALTAR.
         MOVE LIMITE TO ZLIMITE.
         INSPECT PAGEH REPLACING 
                 ALL "##limite##" BY ZLIMITE.
         INSPECT PAGEH REPLACING 
                 ALL "##saltar##" BY ZSALTAR.
         DISPLAY FUNCTION TRIM (PAGEH). 

 

        SQLERRORES.
           MOVE SPACES TO RESULTADO.
           IF SQLCODE < 0 
                      DISPLAY OPERACION,  '*SQLSTATE='  SQLSTATE,
                              ', *SQLCODE=' SQLCODE
              IF SQLERRML > 0
                 DISPLAY 'SQL ERROR MESSAGE:' SQLERRMC(1:SQLERRML)
                 MOVE  SQLERRMC(1:SQLERRML) 
                       TO RESULTADO
              END-IF
              MOVE SQLCODE TO RETURN-CODE
              STOP RUN                                                           
           ELSE IF SQLCODE > 0 AND NOT = 100
                      DISPLAY 'SQLSTATE='  SQLSTATE,
                              ', SQLCODE=' SQLCODE
              IF SQLERRML > 0
                 DISPLAY 'SQL WARNING MESSAGE:' SQLERRMC(1:SQLERRML)
                 MOVE  SQLERRMC(1:SQLERRML)
                       TO RESULTADO
              END-IF
           END-IF.
           IF SQLCODE = 100
              MOVE 'FIN DE TABLA' TO RESULTADO
           END-IF.
           MOVE 'COMMIT' TO BUFFER.
           EXEC SQL 
             EXECUTE IMMEDIATE :BUFFER
           END-EXEC
           .

       END PROGRAM COBUSRLST.

